autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git

zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' use-simple true

zstyle ':vcs_info:*' stagedstr "%F{green}${git_status_visual}%f"
zstyle ':vcs_info:*' unstagedstr "%F{magenta}${git_status_visual}%f"
zstyle ':vcs_info:git+set-message:*' hooks git-include
zstyle ':vcs_info:git*:*' formats '[%b]%m%c%u '
zstyle ':vcs_info:git*:*' actionformats '[%b|%a]%m%c%u '

function +vi-git-include() {
  if [[ -n $(git ls-files --exclude-standard --others 2> /dev/null) ]]; then
    hook_com[unstaged]+="%F{cyan}${git_status_visual}%f"
  fi
}

setopt PROMPT_SUBST

export PS1="%F{cyan}%1~%f%F{yellow}%(?..!)%f%F{magenta}$%f "
export RPROMPT="\${vcs_info_msg_0_}%F{cyan}%~"

autoload -U add-zsh-hook

add-zsh-hook precmd vcs_info
